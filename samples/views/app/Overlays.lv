import live-web.dom
import live-web
import live-elements-web-server.view
import live-elements-web-server.style
import live-web-view.content
import live-web-view.layout
import live-web-view.button
import live-web-view.color
import live-web-view.overlay

component Overlays < PageView{
    static Array use = [
        Content,
        UlV,
        Float,
        IconButton,
        PrimaryButton,
        ContextMenu,
        Dropdown,
        CenterLayout,
        ColorSchemeToggleButton,
        Modal,
        ModalConfirmBox,
        ScopedStyle{ src: '../styles/views.css' process: 'live-web-view/style/CSSProcessor.lv'  }
    ]

    head: PageProperties{
        ColorSchemeLoader{}
    }

    Body{

        classes: [ScopedStyle.className(Blog)]

        Float.('BR'){
            ColorSchemeToggleButton{}
        }

        ContextMenu{ id: contextMenu }
        Modal{ id: modal }

        CenterLayout{
            UlV{
                Li{
                    PrimaryButton{ 
                        T`Context Menu`
                        on click: (e) => {
                            e.preventDefault()
                            contextMenu.show(
                                { x: e.pageX, y: e.pageY },
                                ContextMenuActionsView.create([
                                    { label: 'Item 1', action: () => {}},
                                    { label: 'Item 2', action: () => {}},
                                    { label: 'Item 3', action: () => {}}
                                ])
                            )
                        }
                    }
                }
                Li{
                    PrimaryButton{
                        T`Confirm Message box.`
                        on click: (e) => {
                            e.preventDefault()
                            modal.show(
                                ModalConfirmBox{
                                    title: 'Confirm'
                                    message: 'Confirm this message'
                                    yes: () => { console.log("Confirmed"); modal.hide(); }
                                    no: () => { console.log("Not Confirmed"); modal.hide(); }
                                }
                            )
                        }
                    }
                }
                Li{
                    Dropdown{
                        DropdownToggleButton{
                            T`Dropdown`
                        }
                        DropdownContent{
                            UlV{ border: true
                                Li`Item 1`
                                Li`Item 2`
                                Li`Item 3`
                            }
                        }
                    }
                }
            }
        }

    }
}