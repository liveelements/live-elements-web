import live-web.dom
import live-web.behavior
import live-web
import live-elements-web-server.view
import live-elements-web-server.style
import live-web-view.form
import live-web-view.layout
import live-web-view.button
import live-web-view.color
import live-web-view.navigation
import live-web-view.iframe

component Frame < PageView{
    static Array use = [
        IconButton,
        TopNav,
        Resizable,
        FullScreen,
        ColorSchemeToggleButton,
        ScopedStyle{ src: '../styles/views.css' process: 'live-web-view/style/CSSProcessor.lv'  }
    ]

    head: PageProperties{
        ColorSchemeLoader{}
    }

    Body{ classes: [ScopedStyle.className(Frame)]
        TopNav{ position: 'static'
            NavContent{
                ZoomInButton{ target: frame }
                ZoomOutButton{ target: frame }
                RefreshButton{ target: frame }
                ResizableFillButton{ target: resizable isFilled: true }
                ColorSchemeToggleButton{}
            }
        }
        FullScreen{ extraClasses: ['pad-topnav']
            Resizable{ id: resizable fill: true border: true
                IFrame{
                    id: frame
                    src: '/blog'
                }
                ResizableHandlePoint{}
            }
        }

    }
}