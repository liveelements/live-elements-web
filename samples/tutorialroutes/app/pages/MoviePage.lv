import live-web.dom
import live-web.model
import live-web.clientrouter

import ky from 'ky'

component MovieState < State{
    number idx: 0
    string title: ''
    string description: ''

    static fn fromJSON(ob:Object){
        return MovieState{
            idx = ob.id
            title = ob.title
            description = ob.description
        }
    }
}

component MoviePageState < State{
    MovieState current: null
    Array<MovieState> movies: []
    boolean isLoading: false
    string error: ''

    fn removeCurrentEntry(){
        if ( !this.current )
            return
        
        this.isLoading = true
        ky.post('/api/remove', {json: {id: this.current.idx}}).json()
            .then(data => {
                this.isLoading = false
                if ( data.error ){
                    this.error = data.error
                } else {
                    ClientNavigation.goTo('/')
                }
            }).catch(error => {
                this.isLoading = false
                this.error = error
            })
    }

    fn completed(){
        this.isLoading = true
        ky.get('/api/list').json()
            .then(data => {
                this.movies = data.map(entry => MovieState.fromJSON(entry))
                const routeInfo = ClientNavigation.currentRoute()
                if ( routeInfo.data && routeInfo.data.id ){
                    this.current = this.movies.find(m => m.idx === parseInt(routeInfo.data.id ))
                } else if ( this.movies.length ){
                    this.current = this.movies[this.movies.length - 1]
                }
                this.isLoading = false
            })
            .catch (error => {
                this.error = error
                this.isLoading = false
            })
    }
}

component MoviePage < Div{
    id: moviePage
    classes: ['container']

    MoviePageState state: MoviePageState{}

    Div{ classes: ['sidebar']
        H2{ 
            T{ text: 'Movies' } 
            NavLink`+|/add`
        }
        Ul{
            children: moviePage.state.movies.map(entry => (Li{
                NavLink.(entry.title){ href: `/view/${entry.idx}` }
            }))
        }
    }
    Div{ classes: ['main']
        H1{ T{ text: moviePage.state.current?.title ? moviePage.state.current.title : ''}}
        P{ T{ text: moviePage.state.current?.description ? moviePage.state.current?.description : ''} }
        Button{ 
            classes: ['edit-btn', moviePage.state.current ? '' : 'hidden'] 
            on click: () => { ClientNavigation.goTo(`/edit/${moviePage.state.current.idx}`) }
            T`Edit` 
        }
        Button{ 
            classes: ['delete-btn', moviePage.state.current ? '' : 'hidden'] 
            on click: () => {
                if ( window.confirm(`Are you sure you want to delete '${moviePage.state.current.title}'?`)){
                    moviePage.state.removeCurrentEntry()
                }
            }
            T`Remove` 
        }
    }

    Div{ classes: ['modal', moviePage.state.isLoading ? 'show' : '']
        Div{ classes: ['spinner'] }
    }

    Div{ classes: ['modal', moviePage.state.error ? 'show' : '']
        Div{ classes: ['error-message'] T{ text: moviePage.state.error } }
        on click: () => moviePage.state.error = ''
    }
    
}