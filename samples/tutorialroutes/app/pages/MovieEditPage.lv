import live-web.dom
import live-web.model
import live-web.clientrouter

import ky from 'ky'

component MovieEditPageState < State{
    string title: ''
    string description: ''
    number idx: null
    boolean isLoading: false
    string error: ''

    fn update(title:string, description:string){
        const data = {title, description}
        this.isLoading = true
        ky.post(`/api/update/${this.idx}`, {json: data}).json()
            .then(data => {
                this.isLoading = false
                if ( data.error ){
                    this.error = data.error
                } else {
                    ClientNavigation.goTo('/')
                }
            })
            .catch(error => {
                this.isLoading = false
                this.error = error
            })
    }

    fn completed(){
        this.isLoading = true
        ky.get('/api/list').json()
            .then(data => {
                const routeInfo = ClientNavigation.currentRoute()
                if ( routeInfo.data && routeInfo.data.id ){
                    const current = data.find(m => m.id === parseInt(routeInfo.data.id ))
                    this.title = current.title
                    this.description = current.description
                    this.idx = current.id
                }
                this.isLoading = false
            })
            .catch (error => {
                this.error = error
                this.isLoading = false
            })
    }
}

component MovieEditPage < Div{
    id: movieEditPage
    classes: ['container']

    MovieEditPageState state: MovieEditPageState{}

    Div{ classes: ['main']
        H1`New entry`
        Form{ props = {action: '/submit-movie-url', method: 'post'}
            Div{ classes: ['form-group']
                Label{ props: ({for: 'movie-title'})
                    T`Movie Title:`
                }
                Input{
                    id: movieTitle
                    props = {name: 'movieTitle', required: ''}
                    glid: 'movie-title'
                    type: 'text'
                    value: movieEditPage.state.title
                }
            }
            Div{ classes: ['form-group']
                Label{ props: ({for: 'movie-description'})
                    T`Description:`
                }
                TextArea{ id: movieDescription
                    glid: 'movie-description'
                    props = {name: 'movieDescription', required: ''}
                    value: movieEditPage.state.description
                }
            }
            Button{ 
                classes: ['submit-btn'] 
                props = {type: 'submit'}
                on click: (e) => {
                    e.preventDefault()
                    movieEditPage.state.update(movieTitle.currentValue, movieDescription.currentValue)
                }

                T`Update`
                
            }
        }
    }

    Div{ classes: ['modal', movieEditPage.state.isLoading ? 'show' : '']
        Div{ classes: ['spinner'] }
    }

    Div{ classes: ['modal', movieEditPage.state.error ? 'show' : '']
        Div{ classes: ['error-message'] T{ text: movieEditPage.state.error } }
        on click: () => movieEditPage.state.error = ''
    }
}