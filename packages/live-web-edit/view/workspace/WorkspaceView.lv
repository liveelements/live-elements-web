import live-web.dom
import live-web.model
import live-web.behavior
import live-elements-web-server.view
import live-elements-web-server.style
import live-web-view.form
import live-web-view.layout
import live-web-view.button
import live-web-view.color
import live-web-view.content
import live-web-view.navigation
import live-web-view.iframe

import .view.button
import .view.codeeditor
import .lib.middle

component WorkspaceView < Div{
    id: workspaceView

     static any[] use = [
        IconButton,
        WriteLockButton,
        CompileButton,
        ForkButton,
        TopNav,
        Resizable,
        FullScreen,
        SplitLayout,
        TabLayout,
        TooltipArea,
        CodeEditorView,
        ColorSchemeToggleButton,
        WorkspaceOpenSelector,
        ScopedStyle{ src: './style/workspaceview.css' process: 'live-web-view/style/CSSProcessor.lv'  }
    ]

    Actions actions: Actions.bindFirst(workspaceView.data, workspaceViewActions)
    
    string[] extraClasses = []
    classes: this.extraClasses.concat([ScopedStyle.className(WorkspaceView)])
    Object data
    WorkspaceViewKeymap keymap = WorkspaceViewKeymap{ 
        prepare: () => {
            for ( let i = 0; i < tabLayout.children.length; ++i ){
                if ( tabLayout.children[i] instanceof TabContent ){
                    const tabcontent = tabLayout.children[i]
                    for ( let j = 0; j < tabcontent.children.length; ++j ){
                        const tabpane = tabcontent.children[j]
                        const editor = tabpane.children[0]
                        editor.document.content = editor.editor.state.doc.toString()
                    }
                }
            }
        }
        actions: workspaceView.actions ? workspaceView.actions.actionMap() : ({}) 
    }

    boolean forkEnabled: true
    boolean lockEnabled: true
    DOMElement tabLayout: tabLayout
    DOMElement frame: frame
    Object codeEditorThemes = ({
        light: CodeEditorView.lightTheme,
        dark: CodeEditorView.darkTheme
    })

    Object openWorkspaceHandler = (workspaceView, name) => {
        workspaceView.actions.forkTemplate(name) 
        workspaceView.data.workspaceSelector = false
    }
    Object reloadFrameHandler = (wsView) => {
        if ( wsView.data.workspaceViewUrl !== wsView.data.workspaceName ){
            wsView.data.workspaceViewUrl = wsView.data.workspaceName
            wsView.frame.src = wsView.data.route('/v/' + wsView.data.workspaceViewUrl)
        } else {
            wsView.frame.refresh()
        }
    }

    constructor(data:Object){
        super()
        if ( !data ){
            throw new Error('WorkspaceView constructor requires data argument.')
        }
        
        this{ data = WorkspaceViewData.fromJSON(data) }
    }

    async fn completed(){
        window.addEventListener('message', (event) => {
            if (event.data.type === 'error') {
                this.data.errors = [`Client error: ${event.data.message}`]
            } else if ( event.data.type === 'log' ){
                this.data.appendLogMessage(event.data.message.join(' '))
            }
        }, false)

        window.addEventListener('popstate', async (event) => {
            const currentUrl = window.location.href
            const currentPath = window.location.pathname
            const pathSegments = currentPath.split('/').filter(segment => segment !== '')

            const prevSegment = pathSegments[pathSegments.length - 2]
            const lastSegment = pathSegments[pathSegments.length - 1]

            if ( prevSegment === 'w' ){
                await this.actions.loadWorkspace(lastSegment)
                return
            }
        })

        const currentUrl = window.location.href
        const currentPath = window.location.pathname
        const pathSegments = currentPath.split('/').filter(segment => segment !== '')

        const prevSegment = pathSegments[pathSegments.length - 2]
        const lastSegment = pathSegments[pathSegments.length - 1]

        await this.actions.loadWorkspace(lastSegment ? lastSegment : '-')
        if ( this.data.workspaceName !== '-' )
            this.data.appendHistory(null, this.data.route(`/w/${this.data.workspaceName}`))
    }

    TopNav{ position: 'static'
        NavContent{
            children: [
                TooltipArea{
                    OpenWorkspaceButton{ on click: (e) => { e.preventDefault(); workspaceView.actions.toggleWorkspaceOpenSelector(); } }
                    Tooltip`Open Workspace`
                },
                ...(workspaceView.forkEnabled ? 
                    [TooltipArea{
                        ForkButton{ on click: (e) => { e.preventDefault(); workspaceView.actions.fork() }}
                        Tooltip`Fork`
                    }] 
                    : []
                ),
                Div{ classes: ['loading', workspaceView.data.loadingStatus ? 'show' : ''] 
                    Span{ T{ text: workspaceView.data.loadingStatus && workspaceView.data.loadingStatus.message ? workspaceView.data.loadingStatus.message : '' }} 
                    Div{ classes: ['spinner']} 
                },
                TooltipArea{ extraClasses: [workspaceView.data.loadingStatus ? 'hide' : '']
                    CompileButton{ extraClasses: [workspaceView.data.writeEnabled() ? '' : 'hide'] on click: (e) => { e.preventDefault(); workspaceView.keymap.prepare(); workspaceView.actions.saveAll() }}
                    Tooltip`Save & Compile`
                },

                ...(workspaceView.lockEnabled ? 
                    [TooltipArea{
                        WriteLockButton{ isLocked: workspaceView.data.writeBehavior !== WorkspaceViewData.WriteBehavior.Write on click: () => { workspaceView.actions.lockWrite() }  }
                        Tooltip`Lock Write`
                    }] 
                    : []
                )
            ]
            
        }
        NavContent{
            
            TooltipArea{
                RefreshButton{ on click: () => { 
                    workspaceView.reloadFrameHandler(workspaceView)
                } }
                Tooltip`Refresh`
            }
            
            TooltipArea{
                ZoomInButton{ target: frame }
                Tooltip`Zoom In`
            }
            
            TooltipArea{
                ZoomOutButton{ target: frame }
                Tooltip`Zoom Out`
            }
            
            TooltipArea{
                ResizableFillButton{ target: resizable isFilled: true }
                Tooltip`Resize`
            }
            
            TooltipArea{
                ColorSchemeToggleButton{}
                Tooltip.("Color Scheme"){ extraClasses: ['position-left'] }
            }
        }
    }

    FullScreen{ center: false extraClasses: ['pad-topnav']

        SplitLayout{ id: workspaceSplitLayout
            panes: [
                SplitPane{ extraClasses: ['flex-content']
                    TabLayout{ extraClasses: ['no-b']
                        id: tabLayout

                        on activated: (index) => {
                            workspaceView.data.editors.forEach((editor, i) => {
                                if ( i === index )
                                    editor.isActive = true
                                else
                                    editor.isActive = false
                            })
                            for ( let i = 0; i < tabLayout.children.length; ++i ){
                                if ( tabLayout.children[i] instanceof TabContent ){
                                    const tabcontent = tabLayout.children[i]
                                    const tabpane = tabcontent.children[index]
                                    const editor = tabpane.children[0]
                                    editor.editor.focus()
                                }
                            }
                        }

                        TabHeader{
                            children: workspaceView.data.editors.map(editor => {
                                const document = editor.document
                                return TabButton{ 
                                    T{ text: (document.isDirty ? '*' + document.path : document.path ) }
                                }
                            })
                        }
                        TabContent{ extraClasses: ['no-p', 'bt']
                            children: workspaceView.data.editors.map(editor => {
                                return TabPane{ 
                                    CodeEditorView{ 
                                        id: codeEditorView
                                        Object editorData = editor.editor
                                        Document document = editor.document
                                        readContent: () => { return this.document.content }
                                        themes: workspaceView.codeEditorThemes
                                        number writeBehavior: workspaceView.data.writeBehavior
                                        on writeBehaviorChanged: () => {
                                            if ( this.isReadOnly && codeEditorView.writeBehavior !== WorkspaceViewData.WriteBehavior.ReadOnly ){
                                                this.isReadOnly = false
                                            } else if ( !this.isReadOnly && codeEditorView.writeBehavior === WorkspaceViewData.WriteBehavior.ReadOnly) {
                                                this.isReadOnly = true
                                            }
                                        }
                                        boolean isReadOnly = codeEditorView.writeBehavior === WorkspaceViewData.WriteBehavior.ReadOnly
                                        config : {
                                            updateListener: (v) => { 
                                                if ( v.docChanged ){
                                                    editor.document.isDirty = true 
                                                }
                                            },
                                            language: editor.document.path.endsWith('.css') ? 'css' : 'javascript',
                                            readOnly: codeEditorView.isReadOnly,
                                            keymap: workspaceView.keymap.keymap
                                        }

                                        fn completed(){ 
                                            this.document.on('contentReset', () => this.initEditor())
                                        }
                                    }
                                }
                            })
                        }
                    }
                    Div{ classes: ['error-box', workspaceView.data.errors.length ? 'show' : '']
                        children: workspaceView.data.errors.map(e => { 
                            return Div{ classes: ['error'] T{ text: e } }
                        })
                    }
                },
                SplitPane{
                    SplitLayout{ vertical: true extraClasses: ['split-3-1', workspaceView.data.logMessages.length === 0 ? 'split-one' : '']
                        panes: [
                            SplitPane{ extraClasses: ['flex-content']
                                Resizable{ id: resizable fill: true border: true
                                    IFrame{
                                        id: frame
                                        classes: [workspaceView.data.frameLoading ? 'frame-loading' : '']
                                        string frameSrc: workspaceView.data.workspaceFrameSrc
                                        on frameSrcChanged: () => {  this.src = this.frameSrc  }
                                        on srcChanged: () => {
                                            if ( this.dom )
                                                workspaceView.data.frameLoading = true
                                        }
                                        number frameRev: workspaceView.data.workspaceFrameRev
                                        on frameRevChanged: () => {
                                            this.refresh()
                                        }
                                        on load: (d) => {
                                            workspaceView.data.frameLoading = false
                                        }
                                    }
                                    ResizableHandlePoint{}
                                }

                                Div{ classes: ['iframe-loading', workspaceView.data.frameLoading ? 'show' : '']
                                    Div{ classes: ['loader'] }
                                }
                            },
                            SplitPane{
                                Div{ classes: ['log-view']
                                    H2`Console`
                                    Div{ classes: ['log-messages']
                                        boolean manualScroll: false
                                        children: workspaceView.data.logMessages.map(e => { 
                                            return Div{ classes: ['log-message'] T{ text: e } }
                                        })
                                        on childrenChanged: () => {
                                            if ( this.dom && !this.manualScroll ){ // update scroll
                                                this.dom.scrollTop = this.dom.scrollHeight
                                            }
                                        }
                                        on scroll: () => {
                                            // disable auto scroll if user interacted
                                            const atBottom = this.dom.scrollTop + this.dom.clientHeight >= this.dom.scrollHeight - 1
                                            this.manualScroll = !atBottom
                                        }
                                    }
                                }
                            }
                        ]
                    }
                }
            ]
        }
    }

    WorkspaceOpenSelector{
        id: workspaceOpenSelector
        data : ({
            templates: workspaceView.data.templates
        })
        workspaceForkHandler: (name) => {
            workspaceView.openWorkspaceHandler(workspaceView, name)
        }
        isOpen : workspaceView.data.workspaceSelector
    }

    DOMBehavior{
        domReady: (d) => {
            window.onclick = (e) => {
                if ( workspaceView.data.workspaceSelector ){ 
                    const dom = workspaceOpenSelector.dom
                    if ( dom ){
                        if ( !dom.contains(e.target) ){
                            workspaceView.data.workspaceSelector = false
                        }
                    }   
                }
            }
        }
    }
}