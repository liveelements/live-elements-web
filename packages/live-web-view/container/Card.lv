import live-web.dom
import live-web
import live-elements-web-server.view
import live-elements-web-server.style

component Card < Div{
    id: card

    static Array use = [ 
        ScopedStyle{ src: './style/card.css' process: '../style/CSSProcessor.lv' }, 
        ScopedStyle{ src: '../style/global.css' process: '../style/CSSProcessor.lv' } 
    ]

    static number Vertical = 0
    static number Horizontal = 1

    classes: this.extraClasses.concat([ScopedStyle.className(Card), this.orientation === Card.Vertical ? 'card-row' : 'card-col'])

    number orientation : Card.Vertical
    Array extraClasses: []
    DOMElement image: Span`Image Placeholder`
    DOMElement title: T`Card Title`
    DOMElement description: P{
        T`Lorem ipsum dolor sit amet, consectetur adipiscing elit. Sed do eiusmod tempor incididunt ut labore et dolore magna aliqua.`
        
        Button{ classes: ['card-button']
            T`Read More`
        }
    }

    Div{ classes: ['card-img-placeholder']
        children: [card.image]
    }
    Div{ classes: ['card-body']
        Section{ classes: ['card-title']
            children: [card.title]
        }
        P{ classes: ['card-text']
            children: [card.description]
        }
    }
}