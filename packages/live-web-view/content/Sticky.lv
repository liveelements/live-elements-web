import live-web.dom
import live-web.behavior
import live-elements-web-server.view
import live-elements-web-server.style

component Sticky < Div{

    static any[] use = [ 
        ScopedStyle{ src: './style/sticky.css' process: '../style/CSSProcessor.lv' }, 
        ScopedStyle{ src: '../style/global.css' process: '../style/CSSProcessor.lv' } 
    ]

    string[] extraClasses: []
    classes: this.extraClasses.concat([ScopedStyle.className(Sticky)])

    number offset: 60
    props: { data: { offset: this.offset } }

    DOMBehavior{
        domReady = (d) => {
            function handleScroll() {
                const offset = parseInt(d.dataset.offset)
                const placeholderOffset = d.parentNode.getBoundingClientRect().top + window.scrollY - offset;
                const placeholderOffsetBottom = d.parentNode.getBoundingClientRect().bottom + window.scrollY - d.getBoundingClientRect().height - offset;

                if (window.scrollY > placeholderOffset) {
                    if ( window.scrollY < placeholderOffsetBottom ){
                        d.classList.add('sticky');
                        d.style.top = `${offset}px`
                        d.classList.remove('stick-bottom');
                    } else {
                        d.classList.remove('sticky');
                        d.style.top = `auto`
                        d.classList.add('stick-bottom');
                    }
                } else {
                    d.classList.remove('sticky');
                    d.classList.remove('stick-bottom');
                }
            }

            console.log()

            window.addEventListener('load', function () { handleScroll() })
            window.addEventListener('scroll', handleScroll)
        }
    }
}