import live-web.dom
import live-web.behavior
import live-web
import live-elements-web-server.style

import .button

component ColorSchemeToggleButton < IconButton{
    static Array use = [ 
        ScopedStyle{ src: './style/colorschemetogglebutton.css' process: '../style/CSSProcessor.lv' }, 
        ScopedStyle{ src: '../style/global.css' process: '../style/CSSProcessor.lv' } 
    ]
    
    classes: this.extraClasses.concat([ScopedStyle.className(ColorSchemeToggleButton)])

    props = { data: { scheme: '' } }

    DOMBehavior{
        domReady: (e) => {
             if (window.localStorage.getItem('color-scheme') === 'dark' || (!('color-scheme' in window.localStorage) && window.matchMedia('(prefers-color-scheme: dark)').matches)) {
                e.dataset.scheme = 'dark'
            } else {
                e.dataset.scheme = 'light'
            }

            e.onclick = () => {
                if ( e.dataset.scheme === 'dark' ){
                    document.querySelector('html').classList.remove('dark')
                    window.localStorage.setItem('color-scheme', 'light');
                    e.dataset.scheme = 'light'
                } else {
                    document.querySelector('html').classList.add('dark')
                    window.localStorage.setItem('color-scheme', 'dark');
                    e.dataset.scheme = 'dark'
                }
            }
        }
    }
    
    Svg{ 
        props: ({width: '15', height: '15', viewBox: '0 0 24 24', fill: 'none', xmlns: 'http://www.w3.org/2000/svg'})
        classes: ['light-icon']
        Path{
            props: ({
                d: 'M12 18a6 6 0 100-12 6 6 0 000 12zm0-16v-2m0 20v2M4 12H2m20 0h-2M5.636 5.636l-1.414-1.414m15.556 15.556l-1.414-1.414M5.636 18.364l-1.414 1.414m15.556-15.556l-1.414 1.414', 
                stroke: {'': 'currentColor', width: '2',linecap: 'round',linejoin: 'round'}
            })
        }
    }

    Svg{
        props: ({xmlns: 'http://www.w3.org/2000/svg', width: '15', height: '15', viewBox: '30 0 100 100'})
        classes: ['dark-icon'] 
        Path{
            props: ({d: 'M 50 5 A 45 45 0 0 1 50 95 A 45 30 0 0 0 50 5 Z', fill: 'currentColor'})
        }
    }
}